# Default attributes query
# Selects the rdf:type value and any other property pointing to a skos:Concept
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rico: <https://www.ica.org/standards/RiC/ontology#>
SELECT ?subject ?attribute ?value
WHERE {
	# The rdf:type is always an attribute
	{ 
		?subject a ?value .
		FILTER(?value NOT IN (rico:Agent, rico:RecordResource))
		BIND(rdf:type AS ?attribute)
	}
	# Everything that is a skos:Concept is an attribute by default
	UNION
	{
		?subject ?attribute ?concept .
		?concept a skos:Concept .
		?concept skos:prefLabel ?value FILTER(lang(?value) = "fr")
	}
}	